# Maintainer: Hal Gentz <zegentzy@protonmail.com>
# Contributor: Alexandre Demers <alexandre.f.demers@gmail.com>

_pkgbase=libomxil-bellagio
pkgname=lib32-$_pkgbase
pkgver=0.9.3
pkgrel=0
pkgdesc="An opensource implementation of the OpenMAX Integration Layer API"
arch=('x86_64')
url="http://omxil.sourceforge.net"
license=('LGPL')
depends=("lib32-glibc" "$_pkgbase")
makedepends=("lib32-gcc-libs")
source=("http://downloads.sourceforge.net/project/omxil/omxil/Bellagio%200.9.3/${_pkgbase}-${pkgver}.tar.gz"
        fedora-fixes.patch)
md5sums=('a1de827fdb75c02c84e55f740ca27cb8'
         'c34f9facf0cf26171c81f2fc3d562ec6')
groups=('gentz_custom')

prepare() {
    cd ${srcdir}/${_pkgbase}-$pkgver

    # Fixes from fedora repo
    patch -Np1 -i ../fedora-fixes.patch
    sed -e 's/-Werror//' -i configure.ac
    autoreconf -fiv
}

exports_32() {
    export PKG_CONFIG="/usr/bin/pkg-config-32"
    export CFLAGS="$CFLAGS -m32"
    export CXXFLAGS="$CXXFLAGS -m32"
    export LDFLAGS="$LDFLAGS -m32"
}

build() {
    exports_32
    cd ${srcdir}/${_pkgbase}-${pkgver}

    ./configure --prefix=/usr --disable-static --libdir=/usr/lib32

    make
}

package() {
    exports_32
    cd ${srcdir}/${_pkgbase}-${pkgver}
    make DESTDIR="${pkgdir}" install

    rm -rf "$pkgdir"/usr/{bin,include,share}
}
