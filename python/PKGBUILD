# Maintainer: Hal Gentz <zegentzy@protonmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Jason Chu <jason@archlinux.org>

pkgname=('python' 'lib32-python')
pkgver=3.7.3
pkgrel=0
_pybasever=${pkgver%.*}
arch=('x86_64')
license=('custom')
url="http://www.python.org/"
makedepends=(
    'tk' 'sqlite' 'valgrind' 'bluez-libs' 'mpdecimal' 'llvm' 'gdb' 
    'xorg-server-xvfb' 'lib32-gcc-libs' 'lib32-tk' 'lib32-sqlite' 'lib32-libnsl' 
    'lib32-libtirpc' 'lib32-mpdecimal'
    'expat' 'lib32-expat' 'libffi' 'lib32-libffi'
)
source=("https://www.python.org/ftp/python/${pkgver%rc*}/Python-${pkgver}.tar.xz"{,.asc}
        dont-make-libpython-readonly.patch
        0001-compileall-Fix-ddir-when-recursing.patch
        pyconfig.h
        Use-right-mpdecimal.patch
        lib32-distutils-sysconfig.patch
        python-config-32.patch)
sha512sums=('6d9b7c0f1764e0f655a39430a3af6f7b5e3c9b7166c042e780677a54b17ad4ca6d0d9cba262c82b1b70bba8f7c28883dad4cc0d7cc194fc7d2c1b5f4f08a763a'
            'SKIP'
            '2ef96708d5b13ae2a3d2cc62c87b4780e60ecfce914e190564492def3a11d5e56977659f41c7f9d12266e58050c766bce4e2b5d50b708eb792794fa8357920c4'
            'ebd04c3b6d41321b1f0d439d356e0ce463760db55dc64109854c70d017cf56608aa19de9fc4a21bf840795ff202b4703444f9af8074b661780798c17e03089ff'
            'e0df774b373178632963c5744eeeff7e2b460e28c2208c8a7f6ddda7f316d5b2d12576bec52a41a3eed0636339266121a4bb068d4a00ed0050bbd5f9cbf800db'
            'a8ba987239600e43cdb83bbed9810e8d34b1239784dd6d16cbeecb5a4f92440e3656e0622e756eace2e9d2d7bd5ac8b4593538195308f852a2000f168a2c5d4e'
            'c6aeb74260740155b3ccee350230613b91567ae945026ce2a7f1f568cd0092d6fc7c49ad8d217dab3ad2e8b2ebe40b95c45b7e6feda2eb8849217ba6c31d7796'
            'f6119c9fb535e28d19c7500d8ce4859f9fa71738520357f4f7b418af25574380861381e911622b8cb85a21297083a3a96252d01595ba5803374b3f3f09d0e647')
validpgpkeys=('0D96DF4D4110E5C43FBFB17F2D347EA6AA65421D')  # Ned Deily (Python release signing key) <nad@python.org>
groups=('gentz_custom')

prepare() {
    mv Python-${pkgver}{,-64}
    pushd Python-${pkgver}-64/
    # FS#45809
    patch -p1 -i ../dont-make-libpython-readonly.patch

    # FS#59997
    patch -p1 -i ../0001-compileall-Fix-ddir-when-recursing.patch

    # https://bugs.python.org/issue34587
    sed -i -e "s|testCongestion|disabled_&|" Lib/test/test_socket.py

    # FS#23997
    sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python|" Lib/cgi.py

    # Speed up LTO
    sed -i -e "s|-flto |-flto=4 |g" configure configure.ac

    # Ensure that we are using the system copy of various libraries (expat, libffi, and libmpdec),
    # rather than copies shipped in the tarball
    rm -r Modules/expat
    rm -r Modules/_ctypes/{darwin,libffi}*
    rm -r Modules/_decimal/libmpdec
    popd

    cp -r Python-${pkgver}{-64/,-32}

    pushd Python-${pkgver}-32
    patch -p1 -i ../Use-right-mpdecimal.patch

    # Fix hard-coded paths
    sed -i "s|base}/lib|base}/lib32|g" "Lib/sysconfig.py"
    sed -i "s|/include|/lib32/python{py_version_short}/include|g" "Lib/sysconfig.py"
    sed -i "s|lib/|lib32/|g" "Modules/getpath.c"
    sed -i "s|base/lib|base/lib32|g" "Lib/distutils/command/install.py"
    sed -i "s|/include|/lib32/python{py_version_short}/include|g" "Lib/distutils/command/install.py"
    sed -i "s|prefix)/lib|prefix)/lib32|g" "Makefile.pre.in"

    patch -Np1 -i "../lib32-distutils-sysconfig.patch"
  
    # Give the configuration script an extention
    patch -Np1 -i "../python-config-32.patch"
    popd
}

exports_32() {
    export PKG_CONFIG="/usr/bin/pkg-config-32"
    export CFLAGS="$CFLAGS -m32"
    export CXXFLAGS="$CXXFLAGS -m32"
    export LDFLAGS="$LDFLAGS -m32"
}

exports_64() {
    export PKG_CONFIG="/usr/bin/pkg-config"
    export CFLAGS="${CFLAGS/-m32}"
    export CXXFLAGS="${CXXFLAGS/-m32}"
    export LDFLAGS="${LDFLAGS/-m32}"
}

build_python() {
    exports_$1
    pushd Python-${pkgver}-$1

    # PGO should be done with -O3
    export CFLAGS="${CFLAGS/-O2/-O3}"

    local _python_configure_options=(
        --prefix=/usr 
        --enable-shared
        --with-threads
        --with-computed-gotos
        #--enable-optimizations
        --with-lto
        --enable-ipv6
        --with-system-expat
        --with-dbmliborder=gdbm:ndbm
        --with-system-ffi
        --with-system-libmpdec
        --enable-loadable-sqlite-extensions
        --without-ensurepip
    )

    # Disable bundled pip & setuptools
    ./configure \
        "${_python_configure_options[@]}" \
        "${@:2}"

    # Obtain next free server number for xvfb-run; this even works in a chroot environment.
    export servernum=99
    while ! xvfb-run -a -n "$servernum" /bin/true 2>/dev/null; do servernum=$((servernum+1)); done

    LC_CTYPE=en_US.UTF-8 xvfb-run -s "-screen 0 1280x720x24 -ac +extension GLX" -a -n "$servernum" make EXTRA_CFLAGS="$CFLAGS"
    popd
} 

build() {
    build_python "32" \
        --libdir=/usr/lib32 \
        --libexecdir=/usr/lib32 \
        --includedir=/usr/lib32/python${_pybasever}m/include \
        --exec_prefix=/usr/lib32/python${_pybasever}/ \
        --bindir=/usr/bin \
        --sbindir=/usr/bin \
        --with-suffix='-32'

    build_python "64"
}

package_common() {
    exports_$1
    cd Python-${pkgver}-$1

    # Hack to avoid building again
    sed -i 's/^all:.*$/all: build_all/' Makefile

    # PGO should be done with -O3
    export CFLAGS="${CFLAGS/-O2/-O3}"

    make DESTDIR="${pkgdir}" EXTRA_CFLAGS="$CFLAGS" install

    # Why are these not done by default...
    ln -s python3$2               "${pkgdir}"/usr/bin/python$2
    ln -s python3$2-config        "${pkgdir}"/usr/bin/python$2-config

    # some useful "stuff" FS#46146
    install -dm755 "${pkgdir}"/usr/lib$3/python${_pybasever}/Tools/{i18n,scripts}
    install -m755 Tools/i18n/{msgfmt,pygettext}.py "${pkgdir}"/usr/lib$3/python${_pybasever}/Tools/i18n/
    install -m755 Tools/scripts/{README,*py} "${pkgdir}"/usr/lib$3/python${_pybasever}/Tools/scripts/

    # License
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python() {
    pkgdesc="Next generation of the python high-level scripting language"
    depends=('expat' 'bzip2' 'gdbm' 'openssl' 'libffi' 'zlib' 'libnsl')
    optdepends=('python-setuptools'
                'python-pip'
                'sqlite'
                'mpdecimal: for decimal'
                'xz: for lzma'
                'tk: for tkinter')
    provides=('python3')
    replaces=('python3')

    package_common "64"

    # Why are these not done by default...
    ln -s python${_pybasever}.1 "${pkgdir}"/usr/share/man/man1/python.1
    ln -s idle3                 "${pkgdir}"/usr/bin/idle
    ln -s pydoc3                "${pkgdir}"/usr/bin/pydoc

    # pyconfs
    mv "${pkgdir}/usr/include/python${_pybasever}m/pyconfig"{,-64}.h
    install -Dm644 ../pyconfig.h "${pkgdir}/usr/include/python${_pybasever}m/pyconfig.h"
}

package_lib32-python() {
    pkgdesc="Next generation of the python high-level scripting language (32-bit)"
    depends=(
        'python'
        'lib32-expat' 
        'lib32-bzip2' 
        'gdbm' 
        'lib32-openssl'
        'lib32-libffi'
        'lib32-zlib'
        'lib32-libnsl'
    )
    optdepends=(
        'python-setuptools'
        'python-pip'
        'lib32-sqlite'
        'lib32-mpdecimal: for decimal'
        'lib32-xz: for lzma'
        'lib32-tk: for tkinter'
    )
    provides=('lib32-python3')
    replaces=('lib32-python3')

    package_common "32" "-32" "32"

    # clean up shebangs
    find "${pkgdir}"/usr/lib32/python${_pybasever}/ -name '*.py' | xargs sed -i "s|#[ ]*![ ]*/usr/bin/env python$|#!/usr/bin/env python${_pybasever}-32|"

    # No conflicts
    rm -rf "${pkgdir}"/{etc,usr/{share,include}}
    rm "${pkgdir}"/usr/bin/{pydoc*,idle*,2to3*,pyvenv*}

    # Why this is in the wrong place, I don't knpw
    mv "${pkgdir}/usr/lib"{,32}"/python${_pybasever}/config-3.7m-x86_64-linux-gnu"

    # This symlink has the wrong name for some reason
    mv "${pkgdir}/usr/bin/python3"{,-32}"-config"

    # pyconfs
    install -Dm644 pyconfig.h "${pkgdir}/usr/include/python${_pybasever}m/pyconfig-32.h"
}
