# Maintainer: Hal Gentz <zegentzy@protonmail.com>
# Contributor: Lone_Wolf <lonewolf at xs4all dot nl>
# Contributor: Armin K. <krejzi at email dot com>
# Contributor: Kristian Klausen <klausenbusk@hotmail.com>
# Contributor: Egon Ashrafinia <e.ashrafinia@gmail.com>
# Contributor: Tavian Barnes <tavianator@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Antti "Tera" Oja <antti.bofh@gmail.com>
# Contributor: Diego Jose <diegoxter1006@gmail.com>

pkgbase=mesa-git-gentz
pkgname=('mesa-git-gentz' 'lib32-mesa-git-gentz')
pkgver=19.3.0_devel.116236.075a96aa926
pkgrel=1
pkgdesc="an open-source implementation of the OpenGL specification, git version"
arch=('x86_64')
url="https://www.mesa3d.org"
license=('custom')
groups=('gentz_custom')
makedepends=(
    'git' 'python-mako' 'xorgproto' 'libomxil-bellagio' 'ocl-icd'
    'vulkan-icd-loader' 'wayland-protocols' 'meson' 'lib32-gcc-libs'
    'lib32-llvm-git' 'lib32-libxml2' 'lib32-libx11' 'lib32-libvdpau'
    'lib32-libva' 'lib32-elfutils' 'lib32-libxrandr' 'lib32-libgcrypt'
    'lib32-wayland' 'lib32-libelf' 'llvm-git' 'libxml2' 'libx11' 'libvdpau'
    'libva' 'elfutils' 'libxrandr' 'libgcrypt' 'wayland' 'libelf' 'clang-git'
    'libclc' 'glslang' 'lib32-libomxil-bellagio'
)
optdepends=('opengl-man-pages: for the OpenGL API man pages')

source=(
    'git+https://github.com/freedesktop/mesa.git'
    'LICENSE'
    'llvm32.cross'
)
sha512sums=('SKIP'
            '25da77914dded10c1f432ebcbf29941124138824ceecaf1367b3deedafaecabc082d463abcfa3d15abff59f177491472b505bcb5ba0c4a51bb6b93b4721a23c2'
            '14728ffa4abd4c2dccaeb090abe747a7f3f5d6e16d2954644f16a5c7fc33a9408683c928d3dfc8f39db21ecfd30a54cb1bec7cb0851ee6d33a993b212a2ab2d3')

#_meson_flags=(-D b_ndebug=false)

pkgver() {
    cd "${srcdir}/mesa"
    read -r _ver <VERSION
    printf '%s.%s.%s' \
        "${_ver/-/_}" \
        "$(git rev-list --count HEAD)" \
        "$(git rev-parse --short HEAD)"
}

_build_meson() {
    if [[ -f ".build$1.built" ]]; then
        return
    fi

    _exports_$1

    meson setup "${srcdir}/mesa" "${srcdir}/_build$1" \
        "${@:2}" \
        "${_meson_flags[@]}" \
        -D buildtype=plain \
        --wrap-mode=nofallback \
        -D prefix=/usr \
        -D sysconfdir=/etc \
        -D platforms=x11,wayland,drm,surfaceless \
        -D dri-drivers=i915,i965 \
        -D gallium-drivers=radeonsi,virgl,swrast,iris \
        -D vulkan-drivers=amd,intel \
        -D dri3=true \
        -D egl=true \
        -D gallium-extra-hud=true \
        -D vulkan-overlay-layer=true \
        -D gallium-nine=true \
        -D gallium-va=true \
        -D gallium-vdpau=true \
        -D gallium-xvmc=false \
        -D gbm=true \
        -D gles1=true \
        -D gles2=true \
        -D glvnd=false \
        -D glx=dri \
        -D libunwind=true \
        -D llvm=true \
        -D lmsensors=true \
        -D osmesa=gallium \
        -D shared-glapi=true \
        -D valgrind=true \
        -D tools=[]

    meson configure "${srcdir}/_build$1"

    ninja $NINJAFLAGS -C "${srcdir}/_build$1"
    touch ".build$1.built"
}

_exports_32() {
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
    #export CFLAGS="$CFLAGS -m32"
    #export CXXFLAGS="$CXXFLAGS -m32"
    #export LDFLAGS="$LDFLAGS -m32"
}

_exports_64() {
    export PKG_CONFIG_PATH="/usr/lib/pkgconfig"
    #export CFLAGS="${CFLAGS/-m32}"
    #export CXXFLAGS="${CXXFLAGS/-m32}"
    #export LDFLAGS="${LDFLAGS/-m32}"
}

prepare() {
    # So git am works.
    # Yeah, we could use git apply, but I want to be able to run diff and only
    # get the changes for my patches.
    git config --global user.email "zegentzy@protonmail.com"
    git config --global user.name "Hal Gentz"
}

build () {
    _build_meson 64 \
        -D gallium-opencl=icd \
        -D gallium-omx=bellagio

    _build_meson 32 \
        --cross-file "${srcdir}/llvm32.cross" \
        --libdir=/usr/lib32 \
        -D gallium-opencl=disabled \
        -D gallium-omx=disabled
}

_package_common() {
    DESTDIR="${pkgdir}" ninja $NINJAFLAGS -C "${srcdir}/_build$1" install

    # indirect rendering
    ln -s /usr/lib$2/libGLX_mesa.so.0 \
        "${pkgdir}/usr/lib$2/libGLX_indirect.so.0"

	install -Dm644 "${srcdir}/LICENSE" \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_lib32-mesa-git-gentz() {
    pkgdesc+=" (32-bit)"
    depends+=(
        'libomxil-bellagio' 'libclc' 'lib32-libelf' 'lib32-libdrm'
        'lib32-libxxf86vm' 'lib32-libxdamage' 'lib32-libxshmfence'
        'lib32-llvm-libs-git' 'lib32-libunwind' 
        'lib32-wayland' 'lib32-lm_sensors' 'glslang'
    )
    provides+=(
        'lib32-mesa-git' 'lib32-libgl' 'lib32-libegl' 'lib32-libgles' 
        'lib32-libglvnd' 'lib32-libglvnd-git' 'lib32-mesa' 'lib32-opencl-mesa' 
        'lib32-vulkan-intel' 'lib32-vulkan-radeon' 'lib32-libva-mesa-driver' 
        'lib32-mesa-vdpau' 'lib32-vulkan-driver' 'lib32-opengl-driver' 
        'lib32-opencl-driver' 'lib32-ati-dri' 'lib32-intel-dri' 
        'lib32-nouveau-dri' 'lib32-mesa-dri' 'lib32-mesa-libgl'
    )
    conflicts+=(
        'lib32-mesa' 'lib32-opencl-mesa' 'lib32-vulkan-intel'
        'lib32-vulkan-radeon' 'lib32-libva-mesa-driver' 'lib32-mesa-vdpau'
        'lib32-ati-dri' 'lib32-intel-dri' 'lib32-nouveau-dri' 'lib32-mesa-dri' 
        'lib32-mesa-libgl' 'lib32-libglvnd-git' 'lib32-libglvnd' 'lib32-libglvnd-git-gentz'
    )

    _package_common "32" "32"

    # remove files provided by mesa-git
    rm -rf "${pkgdir}/etc"
    rm -rf "${pkgdir}/usr/include"
    rm -rf "${pkgdir}/usr/share/glvnd/"
    rm -rf "${pkgdir}/usr/share/drirc.d/"
    rm -rf "${pkgdir}/usr/share/vulkan/explicit_layer.d/"
}

package_mesa-git-gentz() {
    depends+=(
        'lib32-libomxil-bellagio' 'libclc' 'libelf' 'libdrm' 'libxxf86vm'
        'libxdamage' 'libxshmfence' 'llvm-libs-git' 'libunwind' 
        'wayland' 'lm_sensors' 'glslang'
    )
    provides+=(
        'mesa-git' 'libgl' 'libegl' 'libgles' 'libglvnd' 'libglvnd-git'
        'mesa' 'opencl-mesa' 'vulkan-intel' 'vulkan-radeon' 'libva-mesa-driver'
        'mesa-vdpau' 'vulkan-driver' 'opengl-driver' 'opencl-driver'
        'ati-dri' 'intel-dri' 'nouveau-dri' 'svga-dri' 'mesa-dri' 'mesa-libgl'
    )
    conflicts+=(
        'mesa' 'opencl-mesa' 'vulkan-intel' 'vulkan-radeon' 'libva-mesa-driver'
        'mesa-vdpau' 'ati-dri' 'intel-dri' 'nouveau-dri' 'svga-dri' 'mesa-dri' 
        'mesa-libgl' 'libglvnd' 'libglvnd-git' 'libglvnd-git-gentz'
    )

    _package_common "64" ""
}
