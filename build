#!/bin/zsh -x

# This code was once commented, but got lost from a call to `ln` in the wrong
# directory. This version has been pieced together from one shity log.

sudo -v || exit $?
while true; do
    sudo -nv
    sleep 1m
    kill -0 $$ 2>/dev/null || exit
done &

local SHOULD_RESUME=false
local SHOULD_RESUME_AFTER=false
local RESUME_PKG=
local PACKAGES=()
local EXCLUDED_PACKAGES=()
local FIRST_PARAMS=()
local EVERY_PARAMS=()
local ONE_ONLY=false
local BACKUP_DIR=
local RESUME_DIR=
local CHROOT_NAME=
local LOOP_DEVICE_SIZE=50G

source "$(pwd)/build_utils"

while [[ $# -gt 0 ]]; do
	case "$1" in
		-r)
            if [[ "$SHOULD_RESUME" != "false" ]]; then
                echo -r conflicts with either other -r or -rp 
                exit 1
            fi
            SHOULD_RESUME=true
			shift # past argument
		;;
		-rp)
            if [[ "$SHOULD_RESUME" != "false" ]]; then
                echo -rp conflicts with either other -rp or -r
                exit 1
            fi
            SHOULD_RESUME=true
            RESUME_PKG="$2"
			shift # past argument
			shift # past value
		;;
        -ra)
            SHOULD_RESUME_AFTER=true
			shift # past argument
        ;;
		-p)
            PACKAGES+=("$2")
			shift # past argument
			shift # past value
		;;
		-e)
            EXCLUDED_PACKAGES+=("$2")
			shift # past argument
			shift # past value
		;;
		-fp)
            FIRST_PARAMS+=("$2")
			shift # past argument
			shift # past value
		;;
		-ep)
            EVERY_PARAMS+=("$2")
			shift # past argument
			shift # past value
		;;
		-o)
            ONE_ONLY=true
			shift # past argument
		;;
		-rs)
            if [[ "$RESUME_DIR" != "" ]]; then
                echo Resume backup dir already passed.
                exit 1
            fi

            RESUME_DIR="$2"
			shift # past argument
			shift # past argument
		;;
		-bk)
            if [[ "$BACKUP_DIR" != "" ]]; then
                echo Do backup dir already passed.
                exit 1
            fi

            BACKUP_DIR="$2"
			shift # past argument
			shift # past argument
		;;
		-cn)
            if [[ "$CHROOT_NAME" != "" ]]; then
                echo Chroot name already passed.
                exit 1
            fi

            CHROOT_NAME="$2"
			shift # past argument
			shift # past argument
		;;
        -lds)
            LOOP_DEVICE_SIZE="$2"
			shift # past argument
			shift # past argument
        ;;

        --help)
            echo "Please refer to the README."
            exit
        ;;
		*) # unknown option
			printf "Unkown option \`$1\`\n"
            exit 1
		;;
	esac
done

if [[ "${#EVERY_PARAMS[@]}" == "0" ]]; then
    echo "Falling back to using -w for every package"
    EVERY_PARAMS=("-w")
fi

if [[ "${CHROOT_NAME}" == "" ]]; then
    echo "Falling back to using chroot name of \"chroot\""
    CHROOT_NAME="chroot"
fi

if [[ "${SHOULD_RESUME}" == "true" && "${RESUME_PKG}" == "" ]]; then
    echo "Falling back to using last_pkg"
    RESUME_PKG="$(cat last_pkg.${CHROOT_NAME})"
fi

if [[ "${#PACKAGES[@]}" == "0" ]]; then
    echo "Falling back to built in package list."
    PACKAGES=( 
        "xorgproto-git-gentz"

        "lib32-tk-gentz"
        "lib32-mpdecimal-gentz"
        "python2-gentz"
        "python-gentz"

        "libx11-git-gentz"

        "lib32-libedit-gentz"
        "llvm-git-gentz"

        "openmpi-git-gentz"
        "valgrind-git-gentz"

        "lib32-libomxil-bellagio-gentz"
        "libdrm-git-gentz"
        "mesa-git-gentz"

        "xorg-server-git-gentz"
        "xf86-video-amdgpu-git-gentz"
        "xf86-video-ati-git-gentz"
        "xf86-video-fbdev-git-gentz"
        "xf86-video-vesa-git-gentz"

        "wine-staging-git-gentz"
        "winetricks-git"
    ) 
fi

mkdir -p custom

# If line starts with BUILDENV and this option is "sign" add a "!".
awk '{
    if (/^ *BUILDENV *= *\( */) {
        split($0, a, "(");
        for (i in a) {
            if (i != 1) printf "("
            if (i == 2) {
                split(a[i], b, ")");
                for (j in b) if (j == 1) {
                    split(b[j], c, " ")
                    for(k in c) {
                        if (k != 1) printf " "
                        if(c[k] == "sign") printf "!"
                        printf c[k]
                    }
                } else printf ")" b[j]
            } else printf a[i]
        }
        printf "\n"
    } else print
}' /etc/makepkg.conf > custom/makepkg.conf

if_buildenv_uses() {
    if [[ "$(awk "{
        if (/^ *BUILDENV *= *\\( */) {
            split(\$0, a, \"(\");
            for (i in a) if (i == 2) {
                split(a[i], b, \")\");
                for (j in b) if (j == 1) {
                    split(b[j], c, \" \")
                    for(k in c) if(c[k] == tolower(\"$1\") || c[k] == tolower(\"!$1\")) 
                        printf c[k]
                } 
            } 
        } 
    }" custom/makepkg.conf)" == "$(echo "$1" | awk '{ printf tolower($0) }')" ]]; then
        eval USE_$1=true
    else
        eval USE_$1=false
    fi
}

local USE_DISTCC
local USE_CCACHE
if_buildenv_uses DISTCC
if_buildenv_uses CCACHE

for PKG_TO_BUILD in $PACKAGES; do
    if [[ ! -d "$PKG_TO_BUILD" ]]; then
        echo "Package \"$PKG_TO_BUILD\" is missing"
        exit 1
    fi

    if [[ ! -f "$PKG_TO_BUILD/build" ]]; then
        echo "Package \"$PKG_TO_BUILD\" is missing build script"
        exit 1
    fi
done

local CHROOT_DIR="$(pwd)/chroots/${CHROOT_NAME}/chroot"
local CHROOT_MOUNT="$(pwd)/chroots/${CHROOT_NAME}"
local CHROOT_FILE="$(pwd)/chroots/${CHROOT_NAME}.img"

# Unmount old loopdevices that where kept due to bad cleanup
lsblk -o NAME,MOUNTPOINT -J -n \
    | grep "${CHROOT_MOUNT}" \
    | xargs -I% bash -xc '
        LOOPDEVICE_NAME="$(echo "%" | cut -d":" -f2 | cut -d"," -f1)"
        MOUNT_DIR="$(echo "%" | cut -d":" -f3 | cut -d"}" -f1)"
        sudo umount "$MOUNT_DIR"
        sudo losetup -d "/dev/$LOOPDEVICE_NAME"
    '

clone_file() {
    for CHROOT_DIR2 in "${CHROOT_DIR}"/*; do
        [[ ! -d "${CHROOT_DIR2}" ]] && continue
        sudo cp "$1" "${CHROOT_DIR2}$2"

        sudo chown $3 "${CHROOT_DIR2}$2"
        sudo chmod $4 "${CHROOT_DIR2}$2"
    done
}

local REMAKE_CHROOT=false
[[ "$SHOULD_RESUME" == "false" || ! -f "${CHROOT_FILE}" ]] \
    && REMAKE_CHROOT=true

if [[ "$REMAKE_CHROOT" == "true" ]]; then
    sudo rm -rf "deps.${CHROOT_NAME}"
    mkdir -p "deps.${CHROOT_NAME}"
    mkdir -p "alt_pkgs.${CHROOT_NAME}"
    mkdir -p "chroots"

    rm */*.pkg.tar-namcap.log
    rm */*-build.log*
    rm */*-package*.log*
    rm */*-namcap.log*
    rm */*-prepare.log*
    rm */*-check.log*
    rm */*-pkgver.log*
    rm "last_pkg.${CHROOT_NAME}" || true

    mkdir -p "pkgs.old.${CHROOT_NAME}"
    mkdir -p "pkgs.${CHROOT_NAME}"
    mv "pkgs.${CHROOT_NAME}/"* "pkgs.old.${CHROOT_NAME}"

    rm "${CHROOT_FILE}"
    fallocate -l $LOOP_DEVICE_SIZE "${CHROOT_FILE}" || exit $?
fi

# We use a loop device cause sometimes the partitions have nosuid on them,
# which breaks the /bin/sudo in the chroot when used from outside the chroot,
# which breaks makepkg for some reason.
local LOOP_DEVICE="$(sudo losetup -f)"
sudo losetup "$LOOP_DEVICE" "${CHROOT_FILE}" || exit $?

cleanup_loopdevice() {
    until sudo losetup -d "$LOOP_DEVICE"; do sleep 2; done
}
trap cleanup_loopdevice EXIT

if [[ "$REMAKE_CHROOT" == "true" ]]; then
    sudo mkfs.ext4 "$LOOP_DEVICE" || exit $?
fi

sudo mkdir -p "${CHROOT_MOUNT}"
sudo mount "$LOOP_DEVICE" "${CHROOT_MOUNT}" || exit $?
cleanup_loopdevice2() {
    until sudo umount "${CHROOT_MOUNT}"; do sleep 2; done
    until sudo losetup -d "$LOOP_DEVICE"; do sleep 2; done
}
trap cleanup_loopdevice2 EXIT

if [[ "$REMAKE_CHROOT" == "true" ]]; then
    sudo mkdir -p "$CHROOT_DIR"

    local INSTALL_PACKAGES=(base-devel gcc-multilib zsh)

    [[ "$USE_CCACHE" == "true" ]] \
        && INSTALL_PACKAGES+=(ccache)

    [[ "$USE_DISTCC" == "true" ]] \
        && INSTALL_PACKAGES+=(distcc python procps-ng)

    sudo /usr/bin/mkarchroot \
        -C /etc/pacman.conf \
        -M "$(pwd)/custom/makepkg.conf" \
        -c /var/cache/pacman/pkg \
        "$CHROOT_DIR/root" \
        "${INSTALL_PACKAGES[@]}"

    sudo /usr/bin/arch-nspawn "$CHROOT_DIR/root" \
        /bin/bash -xc 'yes | pacman -Syyu'

    sudo mkdir -p "${CHROOT_DIR}/root/alt_pkgs.${CHROOT_NAME}/"
    sudo cp "alt_pkgs.${CHROOT_NAME}/"*.pkg* "${CHROOT_DIR}/root/pkgs/"
    if [[ "$ALT_DEPS" != "" ]]; then
        sudo /usr/bin/arch-nspawn "${CHROOT_DIR}/root" \
            /bin/bash -xc "yes | pacman -U /pkgs/*"
    fi
else
    clone_file {custom,/etc}/makepkg.conf root:root 644

    for CHROOT_DIR2 in "${CHROOT_DIR}"/*; do
        [[ ! -d "${CHROOT_DIR2}" ]] && continue
        sudo /usr/bin/arch-nspawn "${CHROOT_DIR2}" \
            /bin/bash -xc 'yes | pacman -Syyu'
    done
fi

if [[ "$USE_DISTCC" == "true" ]]; then
    clone_file /etc/distcc/hosts{,} root:root 644
    clone_file /etc/conf.d/distccd{,} root:root 644
    FIRST_PARAMS+=(-dist)
    EVERY_PARAMS+=(-dist)
    
    for CHROOT_DIR2 in "${CHROOT_DIR}"/*; do
        [[ ! -d "${CHROOT_DIR2}" ]] && continue
        sudo ln -s /usr/bin/distcc "${CHROOT_DIR2}/usr/lib/distcc/bin/clang++"
        sudo ln -s /usr/bin/distcc "${CHROOT_DIR2}/usr/lib/distcc/bin/clang"
        sudo ln -s /usr/bin/distcc "${CHROOT_DIR2}/usr/lib/distcc/clang++"
        sudo ln -s /usr/bin/distcc "${CHROOT_DIR2}/usr/lib/distcc/clang"
    done
fi

[[ "$RESUME_DIR" != "" ]] && mkdir -p "$RESUME_DIR"
[[ "$BACKUP_DIR" != "" ]] && mkdir -p "$BACKUP_DIR"

for PKG_TO_BUILD in $PACKAGES; do
    if [[ "$SHOULD_RESUME" != "false" ]]; then
        if [[ "$PKG_TO_BUILD" != "$RESUME_PKG" ]]; then 
            echo "Skipping $PKG_TO_BUILD"
            continue
        fi
        SHOULD_RESUME=false
        if [[ "$SHOULD_RESUME_AFTER" == "true" ]]; then
            continue
        fi
    fi

    if array_contains_element $PKG_TO_BUILD ${EXCLUDED_PACKAGES[@]} ; then 
        echo "Excluding $PKG_TO_BUILD"
        continue
    fi

    echo "Doing $PKG_TO_BUILD"

    echo "$PKG_TO_BUILD" > "last_pkg.${CHROOT_NAME}"

    local PARAMS=(${EVERY_PARAMS[@]})

    if [[ "${#FIRST_PARAMS[@]}" != "0" ]]; then
        PARAMS=("${FIRST_PARAMS[@]}")
        FIRST_PARAMS=()
    fi

    array_contains_element "-rs" ${PARAMS[@]} \
        && echo "Did not insert -rs as already present" \
        || {
            [[ "$RESUME_DIR" != "" ]] \
                && PARAMS+=("-rs" "$RESUME_DIR/$PKG_TO_BUILD")
        }

    if array_contains_element "-rs" ${PARAMS[@]} ; then 
        # Strip -w, as that conflicts
        TO_REMOVE=("-w")
        remove_from_array PARAMS TO_REMOVE
        PARAMS=("${RFA_RETURN[@]}")
    fi

    array_contains_element "-bk" ${PARAMS[@]} \
        && echo "Did not insert -bk as already present" \
        || {
            [[ "$BACKUP_DIR" != "" ]] \
                && PARAMS+=("-bk" "$BACKUP_DIR/$PKG_TO_BUILD")
        }

    cd "$PKG_TO_BUILD"
    ./build "${PARAMS[@]}" -c "$CHROOT_DIR" -cn "$CHROOT_NAME" || {
        echo "Failed to build $PKG_TO_BUILD"
        exit 1
    }

    cd ..
    echo "Done $PKG_TO_BUILD"

    if [[ "$ONE_ONLY" == "true" ]]; then
        exit
    fi
done
