# Maintainer: Hal Gentz <zegentzy@protonmail.com>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Anatol Pomozov <anatol dot pomozov at gmail>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

_pkgname=openmpi
pkgname="${_pkgname}-git-gentz"
pkgver=4.1.0.a1.r30084.r5e9d07d4e0
pkgrel=4
pkgdesc='High performance message passing library (MPI)'
url='https://www.open-mpi.org'
arch=('x86_64')
license=('custom:OpenMPI')
provides=('openmpi' 'openmpi-git')
conflicts=('openmpi' 'openmpi-git')
depends=('libltdl' 'hwloc' 'openssh' 'zlib' 'libnl')
makedepends=('inetutils' 'valgrind' 'gcc-fortran' 'git')
optdepends=('gcc-fortran: fortran support')
options=('staticlibs')
source=("${_pkgname}::git+https://github.com/open-mpi/ompi.git")
sha256sums=('SKIP')
sha512sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	. VERSION
	printf "$major.$minor.$release.$greek.r%s.r%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	cd "${srcdir}/${_pkgname}"
    ./autogen.pl
    ./configure --prefix=/usr \
        --sysconfdir="/etc/${_pkgname}" \
        --enable-mpi-fortran=all \
        --libdir="/usr/lib/${_pkgname}" \
        --with-threads=posix \
        --enable-mpi-thread-multiple \
        --enable-smp-locks \
        --enable-builtin-atomics \
        --enable-mpi-cxx \
        --with-valgrind \
        --enable-memchecker \
        --enable-mpi1-compatibility \
        --enable-pretty-print-stacktrace \
        --without-slurm \
        --with-hwloc=/usr \
        --with-libltdl=/usr \
        FC=/usr/bin/gfortran \
        LDFLAGS="${LDFLAGS} -Wl,-z,noexecstack"
}

build() {
	cd "${srcdir}/${_pkgname}"
    make
}

check() {
	cd "${srcdir}/${_pkgname}"
    make check
}

package() {
	cd "${srcdir}/${_pkgname}"
    make DESTDIR="${pkgdir}" install

    # FS#28583
    install -dm 755 "${pkgdir}/usr/lib/pkgconfig"
    for i in ompi-c.pc ompi-cxx.pc ompi-f77.pc ompi-f90.pc ompi.pc; do
        ln -sf "/usr/lib/openmpi/pkgconfig/${i}" "${pkgdir}/usr/lib/pkgconfig/"
    done

    install -dm 755 "${pkgdir}/etc/ld.so.conf.d"
    echo "/usr/lib/${_pkgname}" > "${pkgdir}/etc/ld.so.conf.d/${_pkgname}.conf"
    install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${_pkgname}"
}
